# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=kmime
pkgver=22.08.1
pkgrel=1
pkgdesc='Library for handling mail messages and newsgroup articles'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(ki18n kcodecs)
makedepends=(extra-cmake-modules doxygen qt5-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0456cbdcec1d6e92147f4688ceb974b0ddee15bad68aee50a785313b067fe5ef'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
