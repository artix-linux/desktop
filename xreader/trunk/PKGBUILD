# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=3.4.4
pkgrel=1
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
groups=(x-apps)
depends=(poppler-glib webkit2gtk xapp)
optdepends=('djvulibre: support for djvu files'
            'libgxps: support for xps files'
            'libspectre: support for dvi and ps files'
            'mathjax2: support for math in epub files'
            'texlive-bin: support for dvi files')
makedepends=(meson samurai intltool itstool gobject-introspection djvulibre
             libgxps libspectre texlive-bin)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2316c5d5e110a1af96121c4930d4d8f759aa538d1fb1e6a248b54732500c2e6e')
b2sums=('9a64756804f9c109224fce557013abc45121ae11285634d2098627f11ad644c1a7996cf06f8eda6be897e60d8c531a34278215bbd46bfc7e31834c346f6fb7c6')

build() {
  artix-meson ${pkgname}-${pkgver} build \
    --libexecdir=lib/${pkgname} \
    -Dmathjax-directory=/usr/share/mathjax2 \
    -Dcomics=true \
    -Ddjvu=true \
    -Ddvi=true \
    -Dt1lib=true \
    -Dpixbuf=true \
    -Dhelp_files=true \
    -Dintrospection=true
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install
}
