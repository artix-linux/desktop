# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime3
pkgver=3.2.12
pkgrel=1
pkgdesc="A C/C++ MIME creation and parser library with support for S/MIME, PGP, and Unix mbox spools"
url="https://github.com/jstedfast/gmime"
arch=(x86_64)
license=(GPL)
depends=(glib2 gpgme zlib libidn2)
makedepends=(gobject-introspection gtk-doc git vala docbook-utils)
provides=(libgmime-3.0.so)
options=(debug)
_commit=41de5940ac1c0877fac2c15a3808e2346559cb98  # tags/3.2.12^0
source=("git+https://github.com/jstedfast/gmime#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gmime
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gmime
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gmime
  ./configure --prefix=/usr \
    --program-prefix=$pkgname \
    --enable-gtk-doc \
    --enable-smime \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd gmime
  make DESTDIR="$pkgdir" install
}
