# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=incidenceeditor
pkgver=22.12.2
pkgrel=1
pkgdesc='KDE PIM incidence editor'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(eventviews pimcommon)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8bb976ee1e1f1afb1c43c94b48b47f45188b59632ae83947bc551cf7429bc463'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  artix-cmake -B build -S $pkgname-$pkgver -DBUILD_TESTING=OFF -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
