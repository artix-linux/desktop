# Maintainer: artist <artist@artixlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesu
pkgver=5.98.0
pkgrel=1
pkgdesc='Integration with su for elevated privileges'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kpty kconfig)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('629fe9bce7c106a6ad9379fd0174e518a0f5b74e1ae2cda985944c17ff59cb95'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# See FS#44277
  chown :nobody "$pkgdir"/usr/lib/kf5/kdesud
  chmod g+s "$pkgdir"/usr/lib/kf5/kdesud
}
