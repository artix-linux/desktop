# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbookmarks
pkgver=5.94.0
pkgrel=1
pkgdesc='Support for bookmarks and the XBEL format'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kxmlgui)
makedepends=(extra-cmake-modules qt5-tools qt5-doc doxygen)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8ccb99ab6f6e9babd503abd043ab183b849cfd9248d3b7d4e2805c75735f517c'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
