# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=akonadi-calendar
pkgver=22.08.1
pkgrel=1
pkgdesc='Akonadi calendar integration'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmailtransport kcalutils akonadi-contacts)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6547cdf8d13d095fe95828f0d7f17cbab6c3e14c3e0454f502380b7fe3a496f5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
