# Maintainer: Nathan <ndowens@artixlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Donald Carr <sirspudd@gmail.com>
# Contributor: Jake Petroules <jake.petroules@qt.io>

pkgname=qbs
pkgver=2.0.0
pkgrel=1
pkgdesc='Cross platform build tool'
arch=(x86_64)
url='https://wiki.qt.io/Qbs'
license=(LGPL)
depends=(qt6-5compat)
makedepends=(cmake)
source=(https://download.qt.io/official_releases/qbs/$pkgver/qbs-src-$pkgver.tar.gz
        4de3f91a61b78de2097c1f7d7d2e59a78f731cb2.diff)
sha256sums=('d78555691ea732949346860e56f68c7c44e9384011359722b032a49b52dfccfd'
            '9a7d127c4a44fe893f7f86728aca8b94c72bee6e7bb473dcee509a2cca379d3a')

prepare() {
  cd $pkgname-src-$pkgver
  patch -Np1 -i ../4de3f91a61b78de2097c1f7d7d2e59a78f731cb2.diff
}

build() {
  artix-cmake -B build -S $pkgname-src-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TESTS=OFF \
    -DQBS_LIBEXEC_INSTALL_DIR=lib/qbs
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
