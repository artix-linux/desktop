# Maintainer: artist <artist@artixlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-stats
pkgver=5.99.0
pkgrel=1
arch=(x86_64)
pkgdesc='A library for accessing the usage data collected by the activities system'
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('933056fe0be56025f661cb37754d4f790cedf8615d6858b7fc63f31ca6d42033'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
