# Maintainer: Nathan <ndowens@artixlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kded
pkgver=5.100.0
pkgrel=1
pkgdesc='Extensible deamon for providing system level services'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kservice kcrash)
makedepends=(extra-cmake-modules kdoctools)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d2581a5fc79ef9e74d96162d796937a6b13bc62ba2bf705b714d4d16684d9bce'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  #remove systemd service
  rm -r $pkgdir/usr/lib/systemd
}
