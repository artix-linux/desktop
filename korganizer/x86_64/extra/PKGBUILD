# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=korganizer
pkgver=22.04.3
pkgrel=1
pkgdesc='Calendar and scheduling Program'
arch=(x86_64)
url='https://apps.kde.org/korganizer/'
license=(GPL2)
depends=(kdepim-runtime kontactinterface incidenceeditor kuserfeedback)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('09cbbc620eaf587528f0c264c2ce03ad8cf2aaf4b986447e6f97280a7263fa7a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
