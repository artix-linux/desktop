# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgbase=ghex
pkgname=(ghex ghex-docs)
pkgver=43.0
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
url="https://wiki.gnome.org/Apps/Ghex"
arch=(x86_64)
license=(GPL)
depends=(libadwaita)
makedepends=(git meson yelp-tools gobject-introspection gi-docgen)
options=(debug)
_commit=2975e977301e44632a2718fb9a74d5f99fdd881f  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/ghex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ghex
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ghex
}

build() {
  artix-meson ghex build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_ghex() {
  provides=(libgtkhex-4.so)
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_ghex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
