# Maintainer: Nathan Owens <ndowens@artixlinux.org>

pkgname=xdg-desktop-portal-gnome
pkgver=42.3
pkgrel=1
pkgdesc="A backend implementation for xdg-desktop-portal for the GNOME desktop environment"
url="https://gitlab.gnome.org/GNOME/xdg-desktop-portal-gnome"
arch=(x86_64)
license=(LGPL)
depends=('xdg-desktop-portal-gtk>=1.10.0-2' libadwaita gnome-keyring
         gnome-desktop-4)
makedepends=(xdg-desktop-portal meson git)
optdepends=('evince: Print previews')
provides=(xdg-desktop-portal-impl)
conflicts=('xdg-desktop-portal-gtk<1.10.0-2')
replaces=('xdg-desktop-portal-gtk<1.10.0-2')
options=(debug)
_commit=725aa7423dd8eed98bcd541967d5655620fdb9e7  # tags/42.3^0
source=("git+https://gitlab.gnome.org/GNOME/xdg-desktop-portal-gnome.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  # Set dummy dir to avoid systemd dep.
  artix-meson $pkgname build -Dsystemduserunitdir=/usr/lib/systemd
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r $pkgdir/usr/lib/systemd
}
