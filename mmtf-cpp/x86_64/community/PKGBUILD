# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mmtf-cpp
pkgver=1.0.0
pkgrel=4
pkgdesc='The pure C++ implementation of the MMTF API, decoder and encoder'
arch=(any)
url='http://mmtf.rcsb.org/'
license=(MIT)
depends=(msgpack-cxx)
makedepends=(cmake)
conflicts=(libmmtf)
provides=(libmmtf)
replaces=(libmmtf)
source=(https://github.com/rcsb/mmtf-cpp/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('881f69c4bb56605fa63fd5ca50842facc4947f686cbf678ad04930674d714f40')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
