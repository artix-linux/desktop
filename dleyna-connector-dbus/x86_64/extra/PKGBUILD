# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-connector-dbus
pkgver=0.4.1
pkgrel=1
pkgdesc="Provides a D-Bus API for the dLeyna services"
url="https://github.com/phako/dleyna-connector-dbus"
arch=(x86_64)
license=(LGPL2.1)
depends=(dleyna-core dbus)
makedepends=(git meson)
_commit=8629619f68703f46d059ce80c6ef68c914f06c83  # tags/v0.4.1^0
source=("git+https://github.com/phako/dleyna-connector-dbus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
