# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: gilbus

pkgname=swayidle
pkgver=1.7.1
pkgrel=3
license=('MIT')
pkgdesc="Idle management daemon for Wayland"
makedepends=(
    'elogind'
    'meson'
    'scdoc'
    'wayland-protocols'
)
depends=(
    'wayland'
    'libelogind'
)
arch=('x86_64')
url="https://github.com/swaywm/swayidle"
options=(debug)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/swaywm/swayidle/archive/$pkgver.tar.gz"
    "https://github.com/swaywm/swayidle/releases/download/$pkgver/$pkgname-$pkgver.tar.gz.sig"
)
sha256sums=('6aa07093be553473726e1ef601dbc5e57a3ea2ac864ef2d7afce1f971ea760a6'
            'SKIP')
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48'  # Simon Ser
              '9DDA3B9FA5D58DD5392C78E652CB6609B22DA89A') # Drew DeVault

build() {
    meson "$pkgname-$pkgver" build \
        --prefix /usr \
        --buildtype=plain \
        -Dlogind=enabled \
        -Dman-pages=enabled \
        -Dlogind-provider=elogind
    ninja -C build
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 "$pkgname-$pkgver/README.md" -t "$pkgdir/usr/share/doc/$pkgname"
}

