# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-taquin
pkgver=3.38.1
pkgrel=1
pkgdesc="Move tiles so that they reach their places"
url="https://wiki.gnome.org/Apps/Taquin"
arch=(x86_64)
license=(GPL3)
depends=(gtk3 librsvg gsound)
makedepends=(meson vala yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=215c4f94521f554fe495874f7776c969e8bb8319  # tags/3.38.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-taquin.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # fix building with vala 0.52
  git cherry-pick -n 99dea5e7863e112f33f16e59898c56a4f1a547b3
  git cherry-pick -n 66be44dc20d114e449fc33156e3939fd05dfbb16
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
