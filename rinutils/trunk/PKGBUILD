# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rinutils
pkgver=0.10.1
pkgrel=1
pkgdesc='C11 / gnu11 utilities C library'
arch=(any)
url='https://www.shlomifish.org/open-source/projects/'
license=(MIT)
depends=()
makedepends=(cmake)
source=(https://github.com/shlomif/rinutils/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('31ec258ce99de7bbbe79f3338ce73048dac45425045eb2bd0d6bd92d1b8b9afb')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TEST_SUITE=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
