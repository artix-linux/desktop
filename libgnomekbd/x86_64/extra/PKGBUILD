# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgnomekbd
pkgver=3.26.1+r5+g54da436
pkgrel=1
epoch=1
pkgdesc="Keyboard management library"
url="https://gitlab.gnome.org/GNOME/libgnomekbd"
arch=(x86_64)
license=(LGPL)
depends=(libxklavier gtk3 dconf)
makedepends=(gobject-introspection git)
provides=(libgnomekbd{,ui}.so)
options=(debug)
_commit=54da436258d79b683f57dfadf787bdf56ca58576  # master
source=("git+https://gitlab.gnome.org/GNOME/libgnomekbd.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 et:
