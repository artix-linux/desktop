# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfwm4
pkgver=4.16.1
pkgrel=3
pkgdesc="Xfce's window manager"
arch=('x86_64')
url="https://docs.xfce.org/xfce/xfwm4/start"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'xfconf' 'libwnck3' 'libepoxy' 'libxpresent'
         'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        client-Handle-removal-of-GTK_FRAME_EXTENTS.patch)
sha256sums=('b5b24ca04bd73c642db0a4b4df81d262381d758f01b51108257d48b391b8718c'
            'cc49d8c0932dc3f630073e87c8fffaddd811306dc32d74a8127fe0de7baccd4d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # https://gitlab.xfce.org/xfce/xfwm4/-/issues/603
  patch -Np1 -i ../client-Handle-removal-of-GTK_FRAME_EXTENTS.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notification \
    --enable-randr \
    --enable-compositor \
    --enable-xsync \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
