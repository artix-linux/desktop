# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Filippo 'JoeyrS' Civiletti <joeyrs@gmail.com>

pkgname=armagetronad
pkgver=0.2.9.1.0
pkgrel=3
pkgdesc='A Tron Clone in 3D.'
arch=('x86_64')
url='http://armagetronad.net/'
license=('GPL')
depends=('sdl_image' 'libxml2' 'sdl_mixer' 'ftgl' 'hicolor-icon-theme')
makedepends=('python')
optdepends=('python: language updater')
#source=("https://downloads.sourceforge.net/sourceforge/armagetronad/armagetronad-$pkgver.src.tar.gz")
source=("https://downloads.sourceforge.net/sourceforge/armagetronad/armagetronad-$pkgver.tbz")
md5sums=('038a7add2ea5868c02dbc5cea7fef533')

prepare() {
     cd "$srcdir/armagetronad-$pkgver"
     2to3 -w language/update.py

     sed -i 's|FTGL.h|ftgl.h|g' configure
     sed -i 's|png_check_sig|png_sig_cmp|g' configure
     sed -i -r '/desktop-id/ s/(<!-- )|(org.armagetronad.)|( -->)//g' desktop/armagetronad.appdata.xml.in
}

build() {
     cd "$srcdir/armagetronad-$pkgver"
     ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --mandir=/usr/share/man \
        --disable-games \
        --enable-automakedefaults \
        --disable-uninstall
     make
}

package() {
     cd "$srcdir/armagetronad-$pkgver"
     make DESTDIR="$pkgdir" install
     install -D -m 644 "desktop/armagetronad.desktop" "$pkgdir/usr/share/applications/armagetronad.desktop"
     for f in 16x16 32x32 48x48; do
       ln -s /usr/share/armagetronad/desktop/icons/$f/armagetronad.png "$pkgdir/usr/share/icons/hicolor/$f/apps/armagetronad.png"
     done
     mv "$pkgdir/usr/bin/armagetronad" "$pkgdir/usr/bin/armagetronad_bin"
     printf "#!/bin/bash\n/usr/bin/armagetronad_bin --configdir /etc/armagetronad --datadir /usr/share/armagetronad" > "$pkgdir/usr/bin/armagetronad"
     chmod +x "$pkgdir/usr/bin/armagetronad"
}
