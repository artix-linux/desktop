# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kimagemapeditor
pkgver=22.04.1
pkgrel=1
arch=(x86_64)
pkgdesc='HTML Image Map Editor'
url='https://apps.kde.org/kimagemapeditor/'
license=(GPL LGPL FDL)
groups=(kde-applications kde-graphics)
depends=(qt5-webengine kparts)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/graphics/kimagemapeditor/-/commit/a411c70d.patch)
sha256sums=('ba7dd3cde5a0ba2cf24aba1122a50cbd1138e3f5320b13bc6200bb47b67af8d3'
            'SKIP'
            '165f60a7d0d987d2d2d2d6014abd2e99aa145dae2fdbb8820957d971b838883a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < a411c70d.patch # Fix Exec line in desktop file
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
