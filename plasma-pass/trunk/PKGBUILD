# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-pass
pkgver=1.2.0
pkgrel=1
arch=(x86_64)
pkgdesc="Plasma applet for the Pass password manager"
url="https://www.dvratil.cz/2018/05/plasma-pass/"
license=(GPL)
depends=(plasma-workspace oath-toolkit)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('01f0b03b99e41c067295e7708d41bbe581c0d73e78d43b50bf86b4699969f780'
            'SKIP')
validpgpkeys=('0ABDFA55A4E6BEA99A83EA974D69557AECB13683') # Daniel Vr√°til <dvratil@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
