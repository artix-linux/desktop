# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-notifyd
pkgver=0.7.2
pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('x86_64')
url="https://docs.xfce.org/apps/notifyd/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python' 'xfce4-panel')
provides=('notification-daemon')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('d841ea11bd69b6dab15c7e6dca62abb2ee22d885ca5e60afb76ea17e628bed7b')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
