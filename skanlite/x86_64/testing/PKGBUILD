# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=skanlite
pkgver=22.12.0
pkgrel=1
pkgdesc='Image Scanning Application'
arch=(x86_64)
url='https://apps.kde.org/skanlite/'
license=(GPL)
groups=(kde-applications kde-graphics)
depends=(libksane kio)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('21e90c42ad3dab91f367faf73b80ec69989eebbf40eaf0ac0761101aa7e5ba10'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
