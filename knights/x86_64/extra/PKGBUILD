# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=knights
pkgver=22.04.3
pkgrel=1
pkgdesc='Chess board by KDE with XBoard protocol support'
arch=(x86_64)
url='https://apps.kde.org/knights/'
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(libkdegames kplotting plasma-framework hicolor-icon-theme)
optdepends=('gnuchess: for playing against the computer')
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ccf17d6f63dcea0cd96d5e70ef7454b3a736d1b1dc6d7b7f4c9f13f38f4b21c2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF   
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
