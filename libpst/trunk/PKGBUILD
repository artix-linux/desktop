# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libpst
pkgname=(libpst libpst-docs)
pkgver=0.6.76
pkgrel=6
pkgdesc="Outlook .pst file converter"
url="https://www.five-ten-sg.com/libpst/"
arch=(x86_64)
license=(GPL)
depends=(gcc-libs libgsf)
makedepends=(python boost)
options=(debug)
source=(https://www.five-ten-sg.com/libpst/packages/${pkgbase}-${pkgver}.tar.gz
        python310.patch)
sha256sums=('3d291beebbdb48d2b934608bc06195b641da63d2a8f5e0d386f2e9d6d05a0b42'
            '8bd2cf189719fa98831b26b28ed68098a82fe6f31a1e23d8576faa27fc4e819f')

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../python310.patch
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man \
      --disable-static --enable-libpst-shared --disable-dii
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_libpst() {
  optdepends=('boost-libs: for libpst python interface')
  provides=(libpst.so)

  make -C $pkgbase-$pkgver DESTDIR="$pkgdir" install

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libpst-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 et:
