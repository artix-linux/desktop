# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin
pkgver=2.7.1
pkgrel=1
pkgdesc='Menu for Xfce4'
arch=('x86_64')
url='https://gottcode.org/xfce4-whiskermenu-plugin/'
groups=('xfce4-goodies')
license=('GPL2')
depends=('garcon' 'xfce4-panel' 'gtk3>=3.22')
makedepends=('cmake' 'ninja')
source=("https://archive.be.xfce.org/src/panel-plugins/xfce4-whiskermenu-plugin/${pkgver%.*}/xfce4-whiskermenu-plugin-$pkgver.tar.bz2")
sha256sums=('04ae0c1764a0d5ec70f18a760d998a2109bb6724f048554d7d6999d9072ca63e')

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-GNinja
  	ninja
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}
