pkgname=pandoc-bin
pkgver=2.18
pkgrel=1
pkgdesc="Pandoc - executable only, without 750MB Haskell depends/makedepends"
url="http://pandoc.org"
license=("GPL2")
arch=('x86_64')
provides=("pandoc")
optdepends=('texlive-core: for pdf output')
conflicts=("pandoc")
replaces=('pandoc-static' 'pandoc-lite')
# The binary release doesn't have the datafiles, so we need to yoink those out of the source tarball, too.
source=("$pkgname-source-$pkgver.tar.gz::https://github.com/jgm/pandoc/archive/${pkgver}.tar.gz")
source_x86_64=("$pkgname-bin-$pkgver.tar.gz::https://github.com/jgm/pandoc/releases/download/${pkgver}/pandoc-${pkgver}-linux-amd64.tar.gz")
#source_aarch64=("$pkgname-bin-$pkgver.tar.gz::https://github.com/jgm/pandoc/releases/download/${pkgver}/pandoc-${pkgver}-linux-arm64.tar.gz")
sha512sums=('44f0d51a37943057385009291315de603f10a8d094fd36146ddc42fd2937413500b77e2df13d4d9d1c6b1a333895887983d3620f5ad2adfbc3b6d681920dee40')
sha512sums_x86_64=('ec0285e9a1640e7594d5bdca3fcdc738b2139e20f4a4f75b0f49ff76d2d2f5a0e8b90912e63a93e787c57f18eb3d155f214f088e55b435ef03acb8cfbf92272b')

package() {
  cd "${srcdir}/pandoc-${pkgver}"

  # To avoid having to download over a gigabyte of haskell makedepends (400-ish for ghc, plus 750 in libs), we
  # just yoink the binary from static compiled binary distributed by pandoc:
  mkdir -p "${pkgdir}/usr/share/pandoc"
  cp -R bin share "${pkgdir}/usr"

  # We're still missing all the datafiles and so on. We get those from the source tarball...
  cp -R data "${pkgdir}/usr/share/pandoc/"
  cp COPYRIGHT MANUAL.txt "${pkgdir}/usr/share/pandoc/"
}

