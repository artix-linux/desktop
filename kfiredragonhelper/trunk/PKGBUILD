pkgname=kfiredragonhelper
pkgver=5.0.6
pkgrel=2
pkgdesc='FireDragon Integration (fork of kmozillahelper from openSUSE).'
url=https://github.com/openSUSE/kmozillahelper
arch=('x86_64')
license=('MIT')
depends=('kio' 'knotifications' 'kwindowsystem' 'ki18n')
makedepends=('cmake' 'extra-cmake-modules' 'git')
source=(git+https://gitlab.com/dr460nf1r3/kfiredragonhelper.git
        kwindowsystem.patch::https://github.com/openSUSE/kmozillahelper/commit/8085be981fbb31da872aa488445d2d076c90c8d4.patch)

prepare() {
	cd $srcdir/$pkgname
	patch -Np1 -i ../kwindowsystem.patch
}


build() {
	mkdir -p build
	cd build
	cmake -G 'Unix Makefiles' -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_TESTING=OFF -DCMAKE_BUILD_TYPE=Release ../${pkgname}
	make
}

package() {
	make -C build DESTDIR="$pkgdir" install
	install -Dm644 "$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums=('SKIP'
            '164ffdede9cfa0877cef79725a0c3a3d553fac570a5f8fbde42811af3bb656bb41d2abc23f47296f13865b51762bf66a372628a042b29c9f482e64bd916105d0')
