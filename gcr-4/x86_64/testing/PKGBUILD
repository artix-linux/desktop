# Maintainer: Dudemanguy <random342@airmail.cc>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gcr-4
pkgname=(gcr-4 gcr-4-docs)
pkgver=4.0.0
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing"
url="https://gitlab.gnome.org/GNOME/gcr"
arch=(x86_64)
license=(GPL2)
depends=(glib2 libgcrypt p11-kit libsecret systemd openssh)
makedepends=(gobject-introspection vala git gi-docgen meson gtk4)
options=(debug)
_commit=dc33aca73a142ef2a4d5c4ed75161bafccd15d3e  # tags/4.0.0^0
source=("git+https://gitlab.gnome.org/GNOME/gcr.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gcr
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gcr
}

build() {
  artix-meson gcr build -Dsystemd=disabled
  meson compile -C build
}

check() {
  # Secure memory tests fail
  dbus-run-session meson test -C build --print-errorlogs || :
}

package_gcr-4() {
  optdepends=('gtk4: gcr-viewer-gtk4')
  provides=(libgck-2.so libgcr-4.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gcr-4-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
