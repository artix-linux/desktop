# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=1.2.1
_tag='9c6d41e4102d2e2e21a6d53ddba38ce202271001' # git rev-parse INKSCAPE_${pkgver/./_}
pkgrel=4
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=('x86_64')
options=('debug')
makedepends=('cmake' 'boost' 'git')

depends=(
	'dbus-glib'
	'double-conversion'
	'gc'
	'graphicsmagick'
	'gsl'
	'gspell'
	'gtkmm3'
	'lcms2'
	'lib2geom'
	'libcdr'
	'libjpeg-turbo'
	'libvisio'
	'libxslt'
	'poppler-glib'
	'potrace'
	'python'
	'python-cssselect'
	'python-lxml'
	'python-numpy'
	'ttf-font'
)

optdepends=(
	'fig2dev: xfig input'
	'gvfs: import clip art'
	'pstoedit: latex formulas'
	'scour: optimized SVG output, some extensions'
	'texlive-core: latex formulas'
)

#source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.bz2")
source=("git+https://gitlab.com/inkscape/inkscape.git#tag=${_tag}"
        inkscape-1.2.1-poppler-22.09.0.patch)
sha256sums=('SKIP'
            'd25ffb560c691398403fc93eb5dde358f4309f95fdce8fd34a57d540e84fc38f')

_backports=(
)

prepare() {
	cd "${pkgname}"
	git submodule update --init --recursive 

	local _c
	for _c in "${_backports[@]}"; do
		git log --oneline -1 "${_c}"
		git cherry-pick -n "${_c}"
	done

	patch -Np1 -i ../inkscape-1.2.1-poppler-22.09.0.patch
}

build() {
	mkdir -p build
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'
	artix-cmake -H${pkgname} -Bbuild \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DWITH_DBUS=ON \

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --build build --target install
}
