# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=42.2
pkgrel=1
pkgdesc='Simple GNOME application to access virtual systems'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Boxes'
license=('LGPL')
groups=('gnome')
depends=('cdrtools' 'edk2-ovmf' 'gtksourceview4' 'gtk-vnc' 'libarchive' 'libgudev' 'libhandy'
         'libosinfo' 'libsecret' 'libvirt-glib' 'mtools' 'qemu-desktop' 'spice-gtk' 'tracker3'
         'webkit2gtk')
makedepends=('appstream-glib' 'gobject-introspection' 'itstool' 'meson' 'spice-protocol' 'vala')
options=('debug')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('f7c2bca14dcae2838db11fa2c80e23408e3548178af861a0d4d0dcc6a9780015')

build() {
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
