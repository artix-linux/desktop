# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-screenshooter
pkgver=1.9.10
pkgrel=2
pkgdesc="An application to take screenshots"
arch=('x86_64')
url="https://docs.xfce.org/apps/screenshooter/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libsoup' 'hicolor-icon-theme')
makedepends=('intltool' 'python')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        do-not-quit-after-dismissing-dialog.patch)
sha256sums=('04b4178527f5b98cfe76ae427e95581067edf262a530639d332f6db9a68292d7'
            '1c4fab5550add18449125f570b1d4c83198963bfeae4495f6896360ea917a789')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # https://gitlab.xfce.org/apps/xfce4-screenshooter/-/issues/71
  patch -Np1 -i ../do-not-quit-after-dismissing-dialog.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
