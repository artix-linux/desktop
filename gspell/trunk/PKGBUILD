# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gspell
pkgver=1.10.0
pkgrel=2
pkgdesc="Spell-checking library for GTK applications"
url="https://gitlab.gnome.org/GNOME/gspell"
arch=(x86_64)
license=(LGPL)
depends=(glib2 gtk3 enchant icu)
makedepends=(gobject-introspection vala gtk-doc autoconf-archive git)
options=(debug)
_commit=59d9cad6f3d1923d87b4ef0352a6539cd6aac52e  # tags/1.10.0^0
source=("git+https://gitlab.gnome.org/GNOME/gspell.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gspell
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gspell
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gspell
  ./configure  --prefix=/usr --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package(){
  depends+=(libg{lib,object,io}-2.0.so libpango-1.0.so libgtk-3.so
            libenchant-2.so libicuuc.so)
  provides+=("libgspell-${pkgver%%.*}.so")

  cd gspell
  make DESTDIR="$pkgdir" install
}
