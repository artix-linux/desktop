# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=5.2.7
pkgrel=2.2
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=(x86_64)
url="https://github.com/linuxmint/cinnamon"
license=(GPL2)
depends=(accountsservice caribou cinnamon-control-center cinnamon-menus cinnamon-screensaver
         cinnamon-session cinnamon-settings-daemon cjs gnome-backgrounds
         gnome-themes-extra gstreamer libcroco libgnomekbd libkeybinder3 librsvg muffin
         network-manager-applet nemo polkit-gnome python-cairo python-dbus
         python-gobject python-pam python-pexpect python-pillow python-pyinotify
         python-pytz python-tinycss2 python-xapp python-requests timezonemap xapps)
optdepends=('blueberry: Bluetooth support'
            'cinnamon-translations: i18n'
            'mintlocale: language selection in settings'
            'gnome-panel: fallback mode'
            'metacity: fallback mode'
            'system-config-printer: printer settings'
            'wget: cover download support in audio applet')
makedepends=(meson samurai gobject-introspection intltool)
options=('!emptydirs')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        cinnamon-meson-0.61.patch::https://patch-diff.githubusercontent.com/raw/linuxmint/cinnamon/pull/10596.patch
        0001-cinnamon-settings-don-t-rely-on-the-presence-of-cinn.patch
        set_wheel.diff
        default-theme.patch)
sha512sums=('62cbbe2f719574b3f70a4895c231be9ad65a41220d2755cc1557bc06bb2b31ced93621c356f2973376d127d5a1e59f9295498bbc4812d428e8605e8f4882c733'
            'de1e06decdd5599e6be2e64e4dcb53d85da795af06d77cc1fe5305e8b9ba0eeeb7cd513afd0feaf7f8eb2b6cbd897769966e037f59f4d0d18e7e86a11c9b5c2f'
            '77c2229d350bb1445375d206af769f1dde9329ce3575e40e9c1d5fea3ec0f49c0a7ef36d78caf895d9224147947bd102bab924b5579dc8dc5f8d3269e0c76374'
            'fd7e117054996ed1c3dfd0f968c2bf98ca4fcee9a100221f8839a232147745ec0140e1f68eeffba58a3c44f66f26e05d433648a7a28858ec669524f7266ba04c'
            'ee7dedd59ea370cf81d75def49060f9a29b22e7b025ca7d5db87a0102d50f138c79aa562b0d36a748c4b1c59a37f600ba1f60ff6caf303cf5b6fc4d110d051b4')
b2sums=('f4ce9ec4c79eab3daadff958c0a334d49757548203078ab657ebe2450f3f179c942df9d0f9089efe313c268c929628d724db43371e12338606d284a2738ed62c'
        '0b20f2419d246db493de55e4146d38dc1f7e58f57db02ba429528d1c31ed532d967195ccfcb41f320b6546f78a27f33e7e820d5595bf5ca2bae1f5cddaebd097'
        '1d873efa0d17e358f834c4374f39bbf3f74340849f9c28a0950c0a064772083798aba3435e564acdf43a94665389917788e295073d4a65cc18600d71b7a9f008'
        '3becf1f40068fc629109e6e7d464c3c484296afacc9ab6328b2ccbb3c9735bcbfa9550f9f73b430ede178ae668e37c660ce322b5b4d1873526de3d3d41185160'
        '86c3a29acd132ca321f08fd81dd5a45707accdc035b2aeec95bf358b29072ff1eedb77b2566cf48d7253d1d791599f0f44938c4600761d711cb18b59019f1c62')

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix build with meson 0.61
  patch -p1 < ../cinnamon-meson-0.61.patch

  # Check for the cc-panel module path, not for the irrelevant binary
  # https://github.com/linuxmint/cinnamon/pull/7382
  patch --no-backup-if-mismatch -p1 -i ../0001-cinnamon-settings-don-t-rely-on-the-presence-of-cinn.patch

  # Use wheel group instread of sudo (taken from Fedora)
  patch -p1 < ../set_wheel.diff

  # Set default theme to 'cinnamon'
  patch -p1 < ../default-theme.patch

  # Replace MintInstall with GNOME Software
  sed -i 's/mintinstall.desktop/org.gnome.Software.desktop/' data/org.cinnamon.gschema.xml

  # Add polkit agent to required components
  sed -i 's/RequiredComponents=\(.*\)$/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
      cinnamon*.session.in

  # https://github.com/linuxmint/cinnamon/issues/3575#issuecomment-374887122
  # Cinnamon has no upstream backgrounds, use GNOME backgrounds instead
  sed -i 's|/usr/share/cinnamon-background-properties|/usr/share/gnome-background-properties|' \
      files/usr/share/cinnamon/cinnamon-settings/modules/cs_backgrounds.py
}

build() {
  mkdir -p build
  cd build
  artix-meson --libexecdir=lib/cinnamon ../${pkgname}-${pkgver}
  samu
}

package() {
  cd build
  DESTDIR="${pkgdir}" samu install
}
