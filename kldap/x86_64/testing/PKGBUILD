# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=kldap
pkgver=22.12.0
pkgrel=1
pkgdesc='LDAP access API for KDE'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kcompletion qtkeychain-qt5)
optdepends=('kio: LDAP kioslave')
makedepends=(extra-cmake-modules kio kdoctools doxygen qt5-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('238cc67ffec52c34d195cd18292ee1aa84ad9de8e21d5170e957c26cc7b46fbd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
