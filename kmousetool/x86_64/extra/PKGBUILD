# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmousetool
pkgver=22.08.3
pkgrel=1
arch=(x86_64)
pkgdesc='Clicks the mouse for you, reducing the effects of RSI'
url='https://apps.kde.org/kmousetool/'
license=(GPL FDL)
groups=(kde-applications kde-accessibility)
depends=(knotifications kxmlgui phonon-qt5 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools libxt)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6d153a2e4fb03628540604bad99ed9f3ca1cff51a9b326b544f380c5083a9b49'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
