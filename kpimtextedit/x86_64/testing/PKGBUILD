# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=kpimtextedit
pkgver=22.04.3
pkgrel=1
pkgdesc='A textedit with PIM-specific features'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kio syntax-highlighting)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('2dbc53584524477e8bbf8f8a919438e47c2ae1345ace5787f2e3989e2db260bd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
