# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=drkonqi
pkgver=5.25.1
pkgrel=1
pkgdesc='The KDE crash handler'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL2)
depends=(kidletime syntax-highlighting kdeclarative kirigami2 kitemmodels gdb)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        no-systemd.patch)
sha256sums=('c0b7e1d7949d629617abdde9aafd6704ddb2b66a253cceaa07ab45a055ffea90'
            'SKIP'
            'b529848e4d4d41675578748e6c8a87bfd875a8ac9af53c988ea5bebcea3de1b5')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
options=(debug)

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../no-systemd.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
