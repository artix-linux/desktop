# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Florian Walch <florian+aur@fwalch.com>

_name=libvterm
pkgname=libvterm01
pkgver=0.1.4
pkgrel=2
pkgdesc='Abstract library implementation of a VT220/xterm/ECMA-48 terminal emulator'
arch=('x86_64')
url='https://www.leonerd.org.uk/code/libvterm/'
license=('MIT')
depends=('glibc')
options=(debug)
source=(
  "https://www.leonerd.org.uk/code/libvterm/libvterm-${pkgver}.tar.gz"
  $pkgname-0.1.4-pkgconf.patch
)
sha256sums=('bc70349e95559c667672fc8c55b9527d9db9ada0fb80a3beda533418d782d3dd'
            'b07067c0d505607a5b2cfa55c2947b859187346b32931fd881c9690aa4ced6d7')

prepare() {
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-0.1.4-pkgconf.patch
}

build() {
  make -C $_name-$pkgver
}

package() {
  make INCDIR=/usr/include/$pkgname LIBDIR=/usr/lib/$pkgname PREFIX=/usr DESTDIR="$pkgdir" install -C $_name-$pkgver
  rm -frv "$pkgdir/usr/"{bin,share}

  install -Dm644 $_name-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set sw=2 sts=2 et:
