# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.40.4
pkgrel=1
epoch=1
pkgdesc="UPnP AV MediaServer and MediaRenderer"
url="https://wiki.gnome.org/Projects/Rygel"
arch=(x86_64)
license=(LGPL)
depends=(gupnp gupnp-av gupnp-dlna libgee gtk3 libunistring tracker3
         libmediaart gst-editing-services)
makedepends=(vala gobject-introspection git meson)
optdepends=('gst-plugins-base: Extra media codecs'
            'gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'tracker3-miners: Share indexed media files'
            'tumbler: Thumbnailing service')
backup=(etc/rygel.conf)
groups=(gnome)
options=(debug)
_commit=d7ffde7f9ab6428782efce59d9755a30e8bfda8e  # tags/rygel-0.40.4^0
source=("git+https://gitlab.gnome.org/GNOME/rygel.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd rygel
  git describe --tags | sed 's/^rygel-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd rygel
  git cherry-pick -n d0410cccf6af55e16d3ba9400ad5e9f703313739
}

build() {
  artix-meson rygel build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
