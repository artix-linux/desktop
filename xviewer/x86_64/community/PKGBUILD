# Maintainer: Chris Cromer <cromer@artixlinux.org>

pkgname=xviewer
pkgver=3.2.12
pkgrel=1
pkgdesc="A simple and easy to use image viewer. X-Apps Project."
arch=('x86_64')
license=('GPL')
depends=('xapp' 'gtk3' 'cinnamon-desktop' 'libpeas' 'libexif' 'libjpeg-turbo'
         'exempi')
makedepends=('meson' 'samurai' 'gobject-introspection' 'librsvg' 'itstool' 'gtk-doc')
optdepends=('xviewer-plugins: Extra plugins'
            'librsvg: for scaling svg images'
            'webp-pixbuf-loader: webp image support')
url='https://github.com/linuxmint/xviewer'

source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
md5sums=('a29cdb23a52afae26bedc9238d6ad87a')

build() {
  artix-meson ${pkgname}-${pkgver} build \
    --libexecdir=lib/${pkgname} \
    --buildtype=plain
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install
}
