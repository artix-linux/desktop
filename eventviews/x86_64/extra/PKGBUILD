# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=eventviews
pkgver=22.08.2
pkgrel=1
pkgdesc='Library for creating events'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(calendarsupport kdiagram libkdepim)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7c7cea74b5f63e3f2e4c48533a7b21e1899b6804e77978fd0e3764059f3a47c2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
