# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=22.04.2
pkgrel=1
pkgdesc='Common libraries for KDE PIM'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(libkdepim kimap kldap akonadi-contacts akonadi-search purpose kpimtextedit)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e141cd0ed14be489d93c784e283db0d3f84fb01f17017a27368b487112de5505'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
