# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: chn <g897331845@gmail.com>

pkgname=libcuckoo
pkgver=0.3
pkgrel=1
pkgdesc="A high-performance, concurrent hash table"
arch=('any')
url='https://github.com/efficient/libcuckoo'
license=('Apache')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/efficient/libcuckoo/archive/refs/tags/v${pkgver}.tar.gz)
md5sums=('13f68f7d20c8b9d819d6f1b12a3b1dc4')

build() {
    mkdir build
    cd build
    cmake ../${pkgname}-${pkgver} \
      -DCMAKE_BUILD_TYPE='None' \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -DBUILD_TESTS=1 \
      -DBUILD_STRESS_TESTS=1 \
      -DBUILD_UNIT_TESTS=1
    make
}

check() {
  cd build
  ctest
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
}
