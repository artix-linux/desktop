# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=kdebugsettings
pkgver=22.04.3
pkgrel=1
pkgdesc='An application to enable/disable qCDebug'
arch=(x86_64)
url='https://apps.kde.org/kdebugsettings/'
license=(GPL)
depends=(kdbusaddons ki18n kitemviews kcompletion kcoreaddons)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ad161555ee63f69c1c644812601cb95976c27b2c5ab05e6f2dab66b19b701ee0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

