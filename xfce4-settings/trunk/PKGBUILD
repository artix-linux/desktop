# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.16.3
pkgrel=1
pkgdesc="Settings manager of the Xfce desktop"
arch=('x86_64')
url="https://docs.xfce.org/xfce/xfce4-settings/start"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'xfconf' 'libnotify' 'libcanberra'
         'colord' 'libxklavier' 'adwaita-icon-theme' 'gnome-themes-extra')
makedepends=('intltool' 'xf86-input-libinput')
optdepends=('python: xfce4-compose-mail -- "mailto:" URI handling')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        $pkgname-fix-ColorSettings-release.patch::https://gitlab.xfce.org/xfce/xfce4-settings/-/commit/4bc8ef847a1a.patch
        default-xsettings-xml.patch)
sha256sums=('f299e6d26d8e142c0b7edeb9b8917d6bd50bc8647254ea585069c68f2bab8e64'
            '551add99a48c7c7863638967194c98e17a4e39bfc391da7b66094957d102dc57'
            '8e9a6c70ab0ceb5d91b637dc290768f8a47edb5d7b6e2eebc4459dbc4ee040d7')
prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://gitlab.xfce.org/xfce/xfce4-settings/-/issues/211
  patch -Np1 -i ../$pkgname-fix-ColorSettings-release.patch

  # Enable Adwaita theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
