# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kross
pkgver=5.95.0
pkgrel=1
pkgdesc='Multi-language application scripting'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kparts qt5-script)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
groups=(kf5-aids)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('557efdd6bc27c93ab2bc0820f74f412395cc8d78248db035ce7923df88b37915'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
