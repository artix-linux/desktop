# Maintainer: Dudemanguy <random342@airmail.cc>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=appstream-glib
pkgver=0.8.1
pkgrel=1
pkgdesc="Objects and methods for reading and writing AppStream metadata"
url="https://people.freedesktop.org/~hughsient/appstream-glib/"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 libyaml pacman gcab curl libstemmer)
makedepends=(gobject-introspection gtk-doc git gperf meson)
provides=(appdata-tools libappstream-glib.so)
conflicts=(appdata-tools)
replaces=(appdata-tools)
options=(debug)
_commit=28c985bec286385504c5303a8f29eab4082f5afd  # tags/appstream_glib_0_8_1^0
source=("git+https://github.com/hughsie/appstream-glib#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^appstream_glib_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D alpm=true
    -D gtk-doc=true
    -D rpm=false
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
