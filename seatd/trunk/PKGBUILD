# Maintainer: Nathan Owens <ndowens@artixlinux.org>

pkgname=seatd
pkgver=0.7.0
pkgrel=2
pkgdesc="A minimal seat management daemon, and a universal seat management library"
arch=(x86_64)
url="https://sr.ht/~kennylevinsen/seatd/"
license=('MIT')
provides=("libseat.so")
replaces=("libseat")
makedepends=(
    'meson'
    'ninja'
    'scdoc'
    'elogind'
)
depends=('libelogind')
options=(debug)
source=(
    "$pkgname-$pkgver.tar.gz::https://git.sr.ht/~kennylevinsen/seatd/archive/$pkgver.tar.gz"
    "sysuser.conf"
)
sha256sums=('210ddf8efa1149cde4dd35908bef8e9e63c2edaa0cdb5435f2e6db277fafff3c'
            'e833292adf5da25b5258a5209c258f0e4c12e1d6fe81cf228fd0cfb575080d78')
build() {
    mkdir build
    meson --prefix=/usr \
          --buildtype=plain \
          -Dexamples="disabled" \
          -Dserver="enabled" \
          -Dlibseat-logind="elogind" \
          -Dman-pages="enabled" \
          "seatd-$pkgver" \
          build
    ninja -C build
}

check() {
    ninja -C build test
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "seatd-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

    install -Dm644 "sysuser.conf" "$pkgdir/usr/lib/sysusers.d/seatd.conf"
}
