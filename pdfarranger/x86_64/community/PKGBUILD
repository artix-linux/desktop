# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alois Nespor <alium at artixlinux.org>

pkgname=pdfarranger
pkgver=1.9.0
pkgrel=2
pkgdesc='Helps merge or split PDF documents and rotate, crop and rearrange pages'
arch=(any)
url='https://github.com/pdfarranger/pdfarranger'
license=('GPL3')
depends=(gtk3 python-gobject python-cairo python-pikepdf python-dateutil ghostscript python-setuptools poppler-glib)
optdepends=('img2pdf: support for image files')
makedepends=(python-distutils-extra)
source=($url/archive/$pkgver/$pkgname-$pkgver.tar.gz
        $pkgname-pikepdf6.patch::$url/commit/6a53d54c7f3e19ca066ea7382f843ad093c80f15.patch)
sha256sums=('dc414e21fdc614b37a11a8ba722b0df37a9ffd66e1cf554d6255d00d1a6d7a9f'
            '07aa8188a3c3addfcce8e5e55d1b75b0bdbf6e688003118ce29887b74379b268')

prepare () {
  patch -p1 -d $pkgname-$pkgver < $pkgname-pikepdf6.patch
}

build () {
  cd $pkgname-$pkgver
  python setup.py build
}

package () {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
