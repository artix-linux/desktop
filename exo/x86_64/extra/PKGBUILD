# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=4.16.3
pkgrel=2
pkgdesc="Application library for Xfce"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4ui' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        $pkgname-exo-open-only-execute-local-desktop-files.patch::https://gitlab.xfce.org/xfce/exo/-/commit/c71c04ff5882.patch)
sha256sums=('722dff3c3fe23f0a65405e63889cf247c99d092d3f9fb16dec78d062cfb8fae6'
            'a864116f1692ca541d4837640e9c535cc127ae2420f64cbafff329f4b4cc68a0')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../$pkgname-exo-open-only-execute-local-desktop-files.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
