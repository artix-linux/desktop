# Maintainer: Nathan <ndowens@artixlinux.org>

pkgname=calendarsupport
pkgver=22.12.1
pkgrel=1
pkgdesc='Calendar support library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kholidays akonadi-calendar akonadi-notes)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('63a9d60b10a02454853339439f5a32a3f8f9c91a25cdb3cf6a892989692de9fb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  artix-cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
