# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=bigsh0t
pkgver=2.5.1
pkgrel=2
pkgdesc='A collection of frei0r plugins for VR video, with support for Shotcut'
arch=(x86_64)
url='https://bitbucket.org/leo_sutic/bigsh0t'
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake python)
source=($pkgname-$pkgver::https://bitbucket.org/leo_sutic/bigsh0t/get/$pkgver.tar.gz)
sha256sums=('7808e1ebd6329f3f52f66c76f3db65571f2422247a1e654f1617efcad3aaa484')

prepare() {
  sed -e 's|-march=native||' -i leo_sutic-$pkgname-*/CMakeLists.txt # Don't use -march=native
}

build() {
  cmake -B build -S leo_sutic-$pkgname-* \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
#  DESTDIR="$pkgdir" cmake --install build
  install -Dm755 build/*.so -t "$pkgdir"/usr/lib/frei0r-1
  install -d "$pkgdir"/usr/share
  mv build/shotcut "$pkgdir"/usr/share
}
