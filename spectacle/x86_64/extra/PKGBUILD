# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=22.04.2
pkgrel=1
pkgdesc='KDE screenshot capture utility'
arch=(x86_64)
url='https://apps.kde.org/spectacle/'
license=(GPL)
depends=(xcb-util-cursor purpose knewstuff kwayland qt5-tools kimageannotator)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-graphics)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1e84a1781cacfc96e7990accbe23a88ea5f8c3597cc283a05907a3273fabf73c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -r $pkgdir/usr/lib/systemd

}
