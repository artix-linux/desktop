# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=squashfuse
pkgver=0.1.104
pkgrel=2
pkgdesc="FUSE filesystem to mount squashfs archives"
arch=(x86_64)
url="https://github.com/vasi/squashfuse"
license=(custom)
depends=(fuse zstd)
makedepends=()
source=($pkgname-$pkgver.tar.gz::https://github.com/vasi/squashfuse/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

# install missing headers
  install -Dm644 *.h -t "$pkgdir"/usr/include/squashfuse
}
sha512sums=('ea3cd5d77d659bc54b63e3bf8084f29e8d8a5ec12dca5e9669e8537c44e53de6e332d52b307188dfb20b2b60a5fbe1e23c0d8813a9cc1d0283f665ec169ce854')
