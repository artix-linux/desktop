# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-connections
pkgver=42.1.2
pkgrel=1
pkgdesc='Remote desktop client for the GNOME desktop environment'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/connections'
license=('GPL3')
depends=('freerdp' 'gtk-vnc' 'libhandy' 'libsecret')
makedepends=('appstream-glib' 'gobject-introspection' 'itstool' 'meson' 'vala')
groups=('gnome-extra')
options=('!emptydirs')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('d8e6e72741091d892dfc84281f625551d0568cd482d48db58a4edb8afa1377b6')

prepare() {
  cd $pkgname-$pkgver
  # Don't use legacy path for metainfo file
  sed -i "s/datadir'), 'appdata/datadir'), 'metainfo/" data/meson.build
  # Add categories to desktop file
  sed -i 's/Categories=GTK;/Categories=GNOME;GTK;Utility;RemoteAccess;Network;/' \
    data/org.gnome.Connections.desktop.in

}

build() {
  artix-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  # Remove unneeded development files
  rm -r "$pkgdir"/usr/include/gnome-connections/gtk-frdp \
        "$pkgdir"/usr/lib/gnome-connections/{girepository-1.0,pkgconfig} \
        "$pkgdir"/usr/share/gnome-connections/{gir-1.0,vapi}
}
