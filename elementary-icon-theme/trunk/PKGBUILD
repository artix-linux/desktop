# Maintainer: Cory Sanin <corysanin@artixlinux.org>
# Contributor: Diego Principe <cdprincipe@gmail.com>
# Contributor: paterbrown <paterbrown@silberhelme.de>
# Contributor: Josip <josipponjavic@gmail.com>

pkgname=elementary-icon-theme
pkgver=7.1.0
pkgrel=1
pkgdesc='Named, vector icons for elementary OS'
arch=(any)
url=https://github.com/elementary/icons
license=(GPL3)
groups=(pantheon)
depends=(hicolor-icon-theme)
makedepends=(
  git
  librsvg
  meson
  xorg-xcursorgen
)
_tag=9b5b27167e4211e76a2ae04223fbc6462e67afbd
source=(elementary-icon-theme::git+https://github.com/elementary/icons.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd elementary-icon-theme

  git describe --tags
}

build() {
  artix-meson elementary-icon-theme build \
    -Dvolume_icons=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
